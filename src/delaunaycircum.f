      SUBROUTINE DELAUNAYCIRCUM(NCC,LCC,N,X,Y,LIST,LPTR,LEND,NT,
     .        CIRCENTRE,LCT,LTRI,IER)
      INTEGER NCC, LCC(*), N, LIST(*), LPTR(*), LEND(N),
     .        NT, LCT(NCC), IER, LTRI(9,*)  
      DOUBLE PRECISION X(*), Y(*), CIRCENTRE(2,*)
C
C     DETERMINE THE CIRCUMCENTRES OF ALL THE TRIANGLES IN THE DELAUNAY TRIANGULATION
c     TO BE USED WITH THE FUNCTIONS FROM TRIPACK.
C     THE TRIANGULATION PRODUCED WITH TRLIST FROM TRIPACK IS ALSO RETURNED
C     
C     Beatriz Pateiro-Lopez <beatriz.pateiro@usc.es>
C
C     ON ENTRY:
C     ... SEE E.G. TRLIST
C
C     ON EXIT:
C     NT   - NUMBER OF TRIANGLES
C     CIRCENTRE - LIST OF CIRCUMCIRCLE CENTERS (2 by NT):
C                 ROW 1,2: X, Y COORDINATES
C     LTRI   LIST OF NEIGHBOUR RELATIONS (9 by NT) see TRLIST:
C            ROW 1-3: NODE INDICES OF TRIANGLE
C            ROW 4-6: NEIGHBOUR TRIANGLE INDICES
C            ROW 7-9: ARC INDICES
C
C     Modules required by DELAUNAYCIRCUM:  TRLIST, CIRCUM

C     DETERMINE ALL CIRCUMCIRCLE CENTERS
      INTEGER NROW, IT, NTRI
      DOUBLE PRECISION X1, X2, X3, Y1, Y2, Y3, XC, YC, SA, AR, 
     .     DEPS, CR
      LOGICAL RATIO
      
      RATIO = .TRUE.
      DEPS = 1.0D-7
      NROW = 9

      CALL TRLIST(NCC,LCC,N,LIST,LPTR,LEND,NROW,NT,LTRI,LCT,IER)
      NTRI=NT

      DO 10 IT = 1,NTRI
         X1 = X(LTRI(1,IT))
         Y1 = Y(LTRI(1,IT))
         X2 = X(LTRI(2,IT))
         Y2 = Y(LTRI(2,IT))
         X3 = X(LTRI(3,IT))
         Y3 = Y(LTRI(3,IT))
         CALL CIRCUM(X1,Y1,X2,Y2,X3,Y3,RATIO,XC,YC,CR,SA,AR)
         CIRCENTRE(1,IT) = XC
         CIRCENTRE(2,IT) = YC
        
 10   CONTINUE

      RETURN
      END
